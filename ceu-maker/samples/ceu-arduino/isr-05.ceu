/*
 * TIMER-OVF => LED, BUTTON => RESTART
 * Runs a timer on OVERFLOW mode, restarting it on every interrupt.
 * Interrupt generates "emit 500ms" to the application.
 * Also uses PIN02 interrupts from the previous example.
 * Main program blinks LED connected to PIN13 every 500ms.
 * Button click freezes the LED for 2s.
 * (http://letsmakerobots.com/node/28278)
 */

native/pre do
    //##define CEU_FEATURES_ISR_SLEEP
end

#include "arduino/avr.ceu"
#include "arduino/arduino.ceu"

input  bool PIN_02;
output bool PIN_13;

spawn async/isr [_digitalPinToInterrupt(2),_CHANGE] do
    emit PIN_02(_digitalRead(2) as bool);
end

_TCCR1A = 0;
_TCCR1B = 0;
_TCNT1  = 34286;                    // preload timer 65536-16MHz/256/2Hz
_TCCR1B = _TCCR1B | (1 << _CS12);   // 256 prescaler
_TIMSK1 = _TIMSK1 | (1 << _TOIE1);  // enable timer overflow interrupt

spawn async/isr [_TIMER1_OVF_vect_num, 0] do
    _TCNT1 = 34286;                     // preload timer 65536-16MHz/256/2Hz
    emit 500ms;
end

loop do
    watching PIN_02 do
        var bool x = false;
        every 500ms do
            x = not x;
            emit PIN_13(x);
        end
    end
    await 2s;
end
